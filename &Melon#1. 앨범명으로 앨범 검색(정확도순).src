[Include]=&Melon#.inc
[IndexUrl]=https://www.melon.com/search/album/index.htm?section=album&searchGnbYn=Y&kkoSpl=N&kkoDpType=RESULT&sort=weight&q=
[AlbumUrl]=https://www.melon.com/album/detail.htm?albumId=
[IndexFormat]=%_coverurl%|%_url%|%Album%|%Artist%|%Title%|%Released%|%Tracks%
[SearchBy]=Album||%album%||%s

[ParserScriptIndex]=...
# 이 부분은 앨범에 대한 웹페이지 검색 결과를 파싱합니다.
# 출력 형식은 상단의 [IndexFormat]에 정의되어 있습니다.
# 검색결과가 여러개일 경우, '검색 결과의 목록...' 대화상자에 표시됩니다.

# 디버깅 용 코드
#debug "on" "debug_melon_search_album.txt"
#debugWriteInput "debug-input_melon_search_album.html"

# 앨범 검색 루프 진입
FindLine "<li class=\"album11_li\">"

Do
  # %_coverurl%: 앨범 커버 URL
  FindLine "WEBPOCIMG.defaultAlbumImg"
  FindInLine "src=\""
  # 쿼리 스트링(?...) 제거
  SayUntil ".jpg"
  Say ".jpg"
  Say "|"

  # %_url%: 앨범 세부정보 멜론 링크(미리보기 버튼)
  # ID 추출
  FindLine "goAlbumDetail"
  FindInLine "goAlbumDetail('"
  SayUntil "');"
  Say "|"

  # %Album%: 앨범명
  # 태그 정리
  RegexpReplace "(<[^>]+>)" ""
  Unspace
  SayRest
  Say "|"

  # %Artist%: 앨범 아티스트(단독, 다수, Various Artists)
  # 여러 아티스트가 여러 줄에 나뉘어 있을 수 있으므로 Join
  FindLine "atistname"
  FindLine "<div"
  JoinUntil "</div>"
  # 중복 데이터 제거
  RegexpReplace "(<span[^>]*class=\"checkEllipsis[^>]*>.*?</span>|<[^>]+>)" ""
  Unspace
  SayRest
  Say "|"

  # %Title%: 타이틀 곡(재생불가능한 곡 포함)
  FindLine "songname"
  RegexpReplace "(<[^>]+>)" ""
  Unspace
  SayRest
  Say "|"

  # %Released%: 발매일
  FindLine "cnt_view"
  RegexpReplace "<[^>]+>" ""
  Unspace
  SayRest
  Say "|"

  # %Tracks%: 앨범 곡 수
  FindLine "tot_song"
  # 숫자만 추출
  SayRegexp "\d+"

  # 다음 루프 이동 (에러 방지: 1 1)
  FindLine "<li class=\"album11_li\">" 1 1
  Unspace

  If "<li class=\"album11_li\">"
    SayNewLine
  Endif
While "<li class=\"album11_li\">"

[ParserScriptAlbum]=...
# 이 스크립트는 앨범 정보 페이지를 파싱하여 모든 정보를 추출합니다.
# 현재 출력 변수는 OutputTo 명령어를 통해 설정됩니다.
# 수집된 모든 필드는 '태그 정보 조정' 대화상자에서 사용됩니다.

# 디버깅 용 코드
#debug "on" "debug_melon_parse_album.txt"
#debugWriteInput "debug-input_melon_parse_album.html"

# coverurl: 앨범 커버
FindLine "<meta property=\"og:image\""
# 고화질(1000px)로 변경
RegexpReplace "(_)(\d+)(.jpg+)" "_1000.jpg"
FindInLine "content=\""
OutputTo "coverurl"
# 쿼리 스트링(?...) 제거
SayUntil ".jpg"
Say ".jpg"

# Album: 앨범명
FindLine "song_name"
JoinUntil "</div>"
# 불필요한 텍스트 제거
RegexpReplace "(<strong[^>]*class=\"none\"[^>]*>.*?</strong>|<[^>]+>)" ""
Unspace
OutputTo "Album"
SayRest

# AlbumArtist: 앨범 아티스트(단독, 다수, Various Artists)
# 여러 아티스트가 여러 줄에 나뉘어 있을 수 있으므로 Join
FindLine "artist"
JoinUntil "</div>"
RegexpReplace "<[^>]+>" ""
Unspace
OutputTo "AlbumArtist"
SayRest

FindLine "<dt>발매일</dt>"
FindLine "<dd"
RegexpReplace "[^\d]" ""
IfGreater 0
  OutputTo "Year"
  # Year: 발매일(20220101 형식)
  IfVar "useDate" "true"
    SayRest
  Else
    # Year: 발매년도
    SayRegexp "^\d{4}"
  Endif
Endif

# Publisher: 발매사
FindLine "<dt>발매사</dt>"
FindLine "<dd"
RegexpReplace "<[^>]+>" ""
Unspace
IfNot "-"
  OutputTo "Publisher"
  SayRest
Endif

# 수록곡 탐색 루프 진입
FindLine "<tr data-group-items=\""

Do
  # DISCNUMBER: 디스크 번호
  FindInLine "cd"
  OutputTo "discTemp"
  # 숫자만 추출(cd1 -> 1)
  SayRegexp "\d+"
  Say "|"

  # Track: 트랙
  FindLine "<span class=\"rank"
  OutputTo "trackTemp"
  # 숫자만 추출
  SayRegexp "\d+" 
  Say "|"

  # Title: 곡
  # 재생 불가능한 곡이 있을 수 있으므로 Join
  FindLine "wrap_song_info"
  JoinUntil "</div>"
  # (Title|HOT|19금) 뱃지 제거
  RegexpReplace "(<span[^>]*class=\"none\"[^>]*>.*?</span>|<[^>]+>)" ""
  Unspace
  OutputTo "titleTemp"
  SayRest
  Say "|"

  # Artist: 아티스트(단독, 다수, Various Artists)
  # 여러 아티스트가 여러 줄에 나뉘어 있을 수 있으므로 Join
  FindLine "ellipsis rank02"
  JoinUntil "</div>"
  # 중복 데이터 제거
  RegexpReplace "(<span[^>]*class=\"checkEllipsis[^>]*>.*?</span>|<[^>]+>)" ""
  Unspace
  OutputTo "artistTemp"
  SayRest
  Say "|"

  # tracks 배열 저장
  OutputTo "tracksTemp"
  Say "|"

  # 다음 루프 이동 (에러 방지: 1 1)
  FindLine "<tr data-group-items=\"" 1 1
  Unspace
While "<tr data-group-items=\""

# 출력
OutputTo "DISCNUMBER"
SayOutput "discTemp"
OutputTo "Track"
SayOutput "trackTemp"
OutputTo "Title"
SayOutput "titleTemp"
OutputTo "Artist"
SayOutput "artistTemp"
OutputTo "Tracks"
SayOutput "tracksTemp"

# 임시 변수 초기화
Set "trackTemp"
Set "titleTemp"
Set "tracksTemp"
Set "discTemp"
Set "artistTemp"